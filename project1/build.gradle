plugins {
    id 'java'
    id 'application'
}

group = 'com.emoseman.cmsc315.project1'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    modularity.inferModulePath = true
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'org.emoseman.cmsc451.project1.Platform'
}

tasks.register('runHotspotOptimized', JavaExec) {
    group = 'application'
    description = 'Runs Platform with aggressive JIT options.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = application.mainClass
    jvmArgs '-XX:CompileThreshold=1000', '-XX:+TieredCompilation'
}

tasks.register('runBenchmarkReport', JavaExec) {
    group = 'application'
    description = 'Launches the Swing BenchmarkReport UI.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.emoseman.cmsc451.project1.BenchmarkReport'
}

dependencies {
    implementation("net.openhft:affinity:3.27ea1")
    implementation 'com.google.code.gson:gson:2.13.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}
